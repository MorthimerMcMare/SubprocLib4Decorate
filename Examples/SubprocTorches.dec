// Almost default Doom tall torches.

Actor SubprocGreenTorch: SubprocActor {
	Radius 16
	Height 68
	ProjectilePassHeight -16
	+SOLID
	+BRIGHT

	States {
	Spawn:
		TGRN ABCD 4 SUBP_Call( "Smoke_subproc" )
		Loop
	Smoke_subproc:
		TNT1 A 0 A_SpawnItemEx( "BulletPuff", FRandom( -radius, radius ) * 0.7, FRandom( -radius, radius ) * 0.7, FRandom( 50, 75 ) )
		TNT1 A 0 SUBP_Return
		Stop
	}
}

Actor SubprocBlueTorch: SubprocGreenTorch {
	States {
	Spawn:
		TBLU ABCD 4 SUBP_Call( "Smoke_subproc" )
		Loop
	}
}

Actor SubprocRedTorch: SubprocGreenTorch {
	States {
	Spawn:
		TRED ABCD 4 SUBP_Call( "Smoke_subproc" )
		Loop
	}
}

Actor SubprocRedTorchOverride: SubprocGreenTorch {
	States {
	Spawn:
		TRED ABCD 4 SUBP_Call( "Smoke_subproc" )
		Loop

	// An analogue of the:
	//
	// "override void Smoke_subproc() {
	//		A_SpawnItemEx( "GrenadeSmokeTrail", ... );
	//		Super.Smoke_subproc();
	// }"
	Smoke_subproc:
		TNT1 A 0 A_SpawnItemEx( "GrenadeSmokeTrail", FRandom( -radius, radius ), FRandom( -radius, radius ), height + FRandom( 5.0, 30.0 ), 0, 0, FRandom( 0.0, 5.0 ) )
		Goto Super::Smoke_subproc
	}
}
