//

class SubproclibStateStackNode play {
	State funcaddr;
	State retaddr;
}

class SubproclibStatesStackKeeper: Inventory {
	const const_StackSize = 256;

	SubproclibStateStackNode subprocStack[ const_StackSize ];
	int stackCurIndex;

	private void StateMessage( String methodname, String msg ) {
		SubproclibGeneral.StateMessage( owner, GetClassName() .. "::" .. methodname, msg );
	}

	static SubproclibStatesStackKeeper GetKeeper( Actor keeperowner ) {
		SubproclibStatesStackKeeper newkeeper = SubproclibStatesStackKeeper( keeperowner.FindInventory( "SubproclibStatesStackKeeper" ) );

		if ( !newkeeper )
			newkeeper = SubproclibStatesStackKeeper( keeperowner.GiveInventoryType( "SubproclibStatesStackKeeper" ) );

		return newkeeper;
	}

	static void RemoveKeeper( Actor keepermaster ) {
		Inventory oldkeeper = keepermaster.FindInventory( "SubproclibStatesStackKeeper" );

		if ( oldkeeper )
			oldkeeper.Destroy();
	}


	State SubprocCall( StateLabel label ) {
		State stsubproc = owner.ResolveState( label );

		if ( !stsubproc ) {
			StateMessage( "SubprocCall", "Cannot resolve a StateLabel" );
			return NULL;
		}

		State outstate = NULL;

		if ( subprocStack[ stackCurIndex ] == NULL )
			subprocStack[ stackCurIndex ] = new( 'SubproclibStateStackNode' );

		if ( stsubproc != subprocStack[ stackCurIndex ].funcaddr ) {
			if ( stackCurIndex < const_StackSize - 1 ) {
				subprocStack[ stackCurIndex ].retaddr = owner.curstate;
				subprocStack[ stackCurIndex ].funcaddr = stsubproc;
				stackCurIndex++;

				subprocStack[ stackCurIndex ] = new( 'SubproclibStateStackNode' );

				outstate = stsubproc;

			} else {
				StateMessage( "SubprocCall", "Call stack overflow, limited at depth " .. stackCurIndex .. "." );
			}
		} else {
			// Forgetting a last subproc:
			subprocStack[ stackCurIndex ].funcaddr = NULL;
		}

		return outstate;
	} // of State SubprocCall( StateLabel label ) {}

	State SubprocReturn( void ) {
		if ( stackCurIndex > 0 )
			return subprocStack[ --stackCurIndex ].retaddr;

		StateMessage( "SubprocReturn", "Call stack underflow." );
		return NULL;
	} // of State SubprocReturn( void ) {}

} // of class SubproclibStatesStackKeeper play {}
