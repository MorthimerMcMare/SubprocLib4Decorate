//

enum EParallelExecDirectives {
	PARSPR_All = 0,		// Apply all sprites.
	PARSPR_SkipTNT1,	// Apply all sprites exculding TNT1A0.
	PARSPR_None,		// Do not apply sprites, use codepointers only.

	PARSPR_Any			= PARSPR_All,
	PARSPR_SkipTNT1A0	= PARSPR_SkipTNT1,
	PARSPR_IgnoreTNT1	= PARSPR_SkipTNT1,
	PARSPR_IgnoreTNT1A0	= PARSPR_SkipTNT1,
	PARSPR_IgnoreAll	= PARSPR_None,
	PARSPR_SkipAll		= PARSPR_None,

	PARSPR_Mask = 0x000F,

	PAREXEC_NoFunctionsCall = 0x0010
}

const PARSTEPS_Infinite = -1;


class SubproclibParallelExecNode play {
	SubproclibParallelExecKeeper keeper;
	Actor master;
	bool frozen;

	State parstate, startstate;
	int curtics;
	int stepsleft;

	EParallelExecDirectives spritesCopyingMode;
	bool noFunctionsCallFlag;

	private SpriteID spriteTNT1A0;

	static SubproclibParallelExecNode Create( SubproclibParallelExecKeeper initkeeper, State initstate, int initsteps, EParallelExecDirectives initparams ) {
		SubproclibParallelExecNode newthread = new( "SubproclibParallelExecNode" );
		newthread.keeper = initkeeper;
		newthread.master = initkeeper.owner;

		newthread.spritesCopyingMode = initparams & PARSPR_Mask;
		newthread.noFunctionsCallFlag = !!( initparams & PAREXEC_NoFunctionsCall );
		newthread.spriteTNT1A0 = Actor.GetSpriteIndex( "TNT1A0" );
		newthread.parstate = newthread.startstate = initstate;
		newthread.stepsleft = ( initsteps == 0? -1 : initsteps );
		newthread.curtics = ( initstate.Tics < 1? initstate.Tics : initstate.Tics + 1 );

		return newthread;
	}

	void ParTick() {
		if ( !master ) {
			Destroy();
			return;
		}

		// 1) Save previous master state data;
		// 2) Apply parallel state sequence;
		// 3) Save new parallel state data;
		// 4) Rollback master changes.

		if ( curtics != -1 && ( --curtics <= 0 ) ) {
			if ( stepsleft > 0 && --stepsleft == 0 ) {
				Destroy();
				return;
			}

			State prevMasterState = master.curstate;
			SpriteID prevMasterSprite = master.Sprite;
			int prevMasterFrame = master.Frame;
			int prevMasterTics = master.tics;

			//console.printf( GetClassName() .. "::Tick(). Prev: state " .. prevMasterState .. ", sprite " .. prevMasterSprite .. ", frame %c, tics " .. prevMasterTics, prevMasterFrame + 65 );

			if ( master.SetState( parstate.NextState, noFunctionsCallFlag ) ) {
				SpriteID newMasterSprite = master.Sprite;
				parstate = parstate.NextState;
				curtics = parstate.Tics;

				master.SetState( prevMasterState, true );

				if ( spritesCopyingMode == PARSPR_None || ( spritesCopyingMode == PARSPR_SkipTNT1A0 && newMasterSprite == spriteTNT1A0 ) ) {
					// Sprite and frame may be changed via codepointers.
					master.Sprite = prevMasterSprite;
					master.Frame = prevMasterFrame;
				} else {
					master.Sprite = parstate.Sprite;
					master.Frame = parstate.Frame;
				}

				master.tics = prevMasterTics;
			} else {
				Destroy();
			}
		} // of if ( curtics != -1 && ( --curtics <= 0 ) ) {}

	} // of void ParTick() {}

} // of class SubproclibParallelExecNode play {}

class SubproclibParallelExecKeeper: Inventory {
	Array<SubproclibParallelExecNode> threads;

	static SubproclibParallelExecKeeper GetKeeper( Actor keepermaster ) {
		SubproclibParallelExecKeeper newkeeper = SubproclibParallelExecKeeper( keepermaster.FindInventory( "SubproclibParallelExecKeeper" ) );

		if ( !newkeeper )
			newkeeper = SubproclibParallelExecKeeper( keepermaster.GiveInventoryType( "SubproclibParallelExecKeeper" ) );

		return newkeeper;
	}

	static void RemoveKeeper( Actor keepermaster ) {
		Inventory oldkeeper = keepermaster.FindInventory( "SubproclibParallelExecKeeper" );

		if ( oldkeeper )
			oldkeeper.Destroy();
	}

	SubproclibParallelExecNode AddThread( StateLabel label, int steps = PARSTEPS_Infinite, EParallelExecDirectives initparams = PARSPR_SkipTNT1A0 ) {
		State stparallel = owner.ResolveState( label );
		SubproclibParallelExecNode newthread = NULL;

		if ( stparallel ) {
			newthread = SubproclibParallelExecNode.Create( self, stparallel, steps, initparams );
			threads.Push( newthread );
		} else {
			SubproclibGeneral.StateMessage( owner, GetClassName() .. "::AddThread()", "Cannot resolve a StateLabel" );
		}

		return newthread;
	} // of SubproclibParallelExecNode AddThread( StateLabel label, EParallelExecDirectives initparams = PARSPR_SkipTNT1A0 ) {}

	int FindThreadByLabel( StateLabel label ) {
		int foundindex = -1;
		State stparallel = owner.ResolveState( label );

		if ( stparallel ) {
			for ( int i = 0; i < threads.Size(); i++ ) {
				if ( threads[ i ] && threads[ i ].startstate == stparallel ) {
					foundindex = i;
					break;
				}
			}
		} // of if ( stparallel ) {}

		return foundindex;
	} // of SubproclibParallelExecNode FindThreadByLabel( StateLabel label ) {}

	int FindThreadByState( State teststate ) {
		int foundindex = -1;

		for ( int i = 0; i < threads.Size(); i++ ) {
			if ( threads[ i ] && threads[ i ].parstate.NextState == teststate ) {
				foundindex = i;
				break;
			}
		}

		return foundindex;
	} // of SubproclibParallelExecNode FindThreadByState( State teststate ) {}

	void RemoveThread( StateLabel label ) {
		int index = 0;

		if ( label != NULL )
			index = FindThreadByLabel( label );
		else
			index = FindThreadByState( owner.curstate );

		if ( index >= 0 ) {
			threads[ index ].Destroy();
			threads.Delete( index, 1 );
		}

		if ( threads.Size() == 0 )
			Destroy();
	} // of void RemoveThread( StateLabel label ) {}

	void ThreadFrozeControl( StateLabel label, bool freezethread ) {
		int index = FindThreadByLabel( label );

		if ( index >= 0 )
			threads[ index ].frozen = freezethread;
	}

	override void Tick() {
		if ( !owner ) {
			Destroy();
			return;
		}

		if ( globalfreeze || level.Frozen )
			return;

		for ( int i = 0; i < threads.Size(); i++ ) {
			if ( !threads[ i ] ) {
				threads.Delete( i, 1 );
				i--;
			} else {
				if ( !threads[ i ].frozen )
					threads[ i ].ParTick();
			}
		}
	} // of override void Tick() {}

	override void OnDestroy() {
		for ( int i = 0; i < threads.Size(); i++ )
			threads[ i ].Destroy();

		Super.OnDestroy();
	}

} // of class SubproclibParallelExecKeeper: Inventory {}

